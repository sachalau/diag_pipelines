rule annotate_with_prokka:
    conda:
        pipeline_path + "envs/prokka.yml"
    input:
        contigs = "samples/{sample}/assembly/spades/coverage_filtered/contigs_500bp_high_coverage.fasta"
    output:
        "samples/{sample}/annotation/{sample}.gff",
        "samples/{sample}/annotation/{sample}.faa",
        "samples/{sample}/annotation/{sample}.fsa",
        "samples/{sample}/annotation/{sample}.txt",
        "samples/{sample}/annotation/{sample}.gbk",
        "samples/{sample}/annotation/{sample}.ffn",
    log:
        "samples/{sample}/annotation/spades/{sample}.log",
    params:
        genus = lambda wildcards: all_samples.loc[wildcards.sample, "ScientificName"].split("_")[0],
        spec = lambda wildcards: all_samples.loc[wildcards.sample, "ScientificName"].split("_")[1]
    shell:
        "prokka --quiet --outdir $( dirname {output[0]}) --force {input[contigs]} --prefix {wildcards.sample} --strain {wildcards.sample} --genus '{params.genus}' --species '{params.spec}' --locustag 'prokka'"

rule annotate_with_prokka_unfiltered_assembly:
    conda:
        pipeline_path + "envs/prokka.yml"
    input:
        contigs = "samples/{sample}/assembly/spades_no_correction/contigs.fasta"
    output:
        "samples/{sample}/annotation/spades_no_correction/{sample}.fsa"
    params:
        genus = lambda wildcards: all_samples.loc[wildcards.sample, "ScientificName"].split("_")[0],
        spec = lambda wildcards: all_samples.loc[wildcards.sample, "ScientificName"].split("_")[1],
    shell:
        "prokka --quiet --outdir $( dirname {output[0]}) --force {input[contigs]} --prefix {wildcards.sample} --strain {wildcards.sample} --genus '{params.genus}' --species '{params.spec}' --locustag 'prokka'"


rule create_blast_database_from_protein_sequences:
    conda:
        pipeline_path + "envs/blast.yml"
    input:
        "samples/{sample}/annotation/{sample}.faa",
    output:
        "samples/{sample}/annotation/{sample}.faa.phr",
    shell:
        """
        makeblastdb -dbtype prot -in {input[0]}
        """

rule create_blast_database_from_contig_sequences:
    conda:
        pipeline_path + "envs/blast.yml"
    input:
        "samples/{sample}/annotation/{sample}.fsa",
    output:
        "samples/{sample}/annotation/{sample}.fsa.nhr",
    shell:
        """
        makeblastdb -dbtype nucl -in {input[0]}
        """

rule remove_fasta_part_from_gff:
    input:
        "samples/{sample}/annotation/{sample}.gff"
    output:
        "samples/{sample}/annotation/{sample}_truncated.gff"
    shell:
        "sed '/^##FASTA$/,$d' {input} > {output}"
    
