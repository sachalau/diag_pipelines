rule calculate_distance_paired_reads_from_refseq_genomes_with_mash:
    conda:
        pipeline_path + "envs/mash.yml"
    input:
        mash_sketch = "references/mash_sketch.msh",
        mash_human = "references/mash_sketch_human.msh",
        R1 = "samples/{sample}/reads/raw/{sample}_R1.fastq.gz",
        R2 = "samples/{sample}/reads/raw/{sample}_R2.fastq.gz",
    output:
        distances = "samples/{sample}/contamination/mash/distances.tsv"
    shell:
        """
        cat {input[R1]} {input[R2]} | mash screen -w  {input[mash_sketch]} - > {output[distances]} 2> /dev/null
        cat {input[R1]} {input[R2]} | mash screen -w  {input[mash_human]} - >> {output[distances]} 2> /dev/null
        """

rule calculate_distance_single_reads_from_refseq_genomes_with_mash:
    conda:
        pipeline_path + "envs/mash.yml"
    input:
        mash_sketch = "references/mash_sketch.msh",
        mash_human = "references/mash_sketch_human.msh",
        single = "samples/{sample}/reads/raw/{sample}_single.fastq.gz",
    output:
        distances = "samples/{sample}/contamination/mash/distances.tsv"
    shell:
        """
        cat {input[single]} | mash screen -w {input[mash_sketch]} - > {output[distances]} 2> /dev/null
        cat {input[single]} | mash screen -w {input[mash_human]} - >> {output[distances]} 2> /dev/null
        """

rule get_taxonomy_from_mash_results:
    conda:
        pipeline_path + "envs/entrez-direct.yml"
    input:
        distances = "samples/{sample}/contamination/mash/distances.tsv"
    output:
        taxonomy = "samples/{sample}/contamination/mash/taxonomy.txt"
    shell:
        """
        sort -gr {input[0]}  | cut -f5  | sed "s/\(GCF_[0-9]\+\.[0-9]\+\)_.*/\\1/" | xargs -I % sh -c "esearch -db assembly -query % | efetch -db assembly -format docsum | xtract -pattern DocumentSummary -element SpeciesName" > {output[taxonomy]} 
        """


rule format_distances_from_mash_results:
    input:
        distances = "samples/{sample}/contamination/mash/distances.tsv"
    output:
        formated_distances = "samples/{sample}/contamination/mash/distances_formated.tsv",
        formated_distances_no_viruses = "samples/{sample}/contamination/mash/distances_formated_no_virus.tsv"        
    shell:
        """
        cat {input[distances]}  | cut -f6 | sed "s/\[\.\.\.]//" | sed "s/\[[0-9]\+ seqs] //" | cut -f2- -d' ' | paste {input[0]} - | cut -f1,2,4,7 | sort -gr  > {output[formated_distances]}
        cat {input[distances]} | cut -f6 | sed "s/\[\.\.\.]//" | sed "s/\[[0-9]\+ seqs] //" | cut -f2- -d' ' | paste {input[0]} - | grep -v "ViralProj" | grep -v "phage" | grep -v "virus" | cut -f1,2,4,7 | sort -gr  > {output[formated_distances_no_viruses]}
        """


rule format_tsv_to_xlsx_mash_results:
    conda:
        pipeline_path + "envs/pandas-openpyxl-pronto-xlrd.yml"
    input:
        tsv = "samples/{sample}/contamination/mash/{results}.tsv",
    output:
        xlsx = "samples/{sample}/contamination/mash/{results}.xlsx",
    script:
        "scripts/format_tsv_to_xlsx.py"

        
rule merge_all_xlsx_mash_results:
    conda:
        pipeline_path + "envs/pandas-openpyxl-pronto-xlrd.yml"
    input:
        xlsx = expand("samples/{sample}/contamination/mash/{{results}}.xlsx", sample=read_naming.keys())
    output:
        xlsx_out = "contamination/{results}.xlsx",
    script:
        pipeline_path + "rules/annotation/resistance/scripts/merge_non_empty_results.py"

