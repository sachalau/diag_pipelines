rule correct_error_paired_reads_with_spades:
    threads:
        16
    conda:
        pipeline_path + "envs/spades.yml"
    input:
        R1_trimmed = "samples/{sample}/reads/trimmed/R1_paired.fastq",
        R2_trimmed = "samples/{sample}/reads/trimmed/R2_paired.fastq",
    output:
        R1_corrected = temp("samples/{sample}/reads/corrected/R1_paired.00.0_0.cor.fastq.gz"),
        R2_corrected = temp("samples/{sample}/reads/corrected/R2_paired.00.0_0.cor.fastq.gz"),
        single_corrected = temp("samples/{sample}/reads/corrected/R_unpaired.00.0_0.cor.fastq.gz"),
    log:
        logging_folder + "samples/{sample}/logs/spades_read_correction.txt"
    shell:
         """
         spades.py --only-error-correction -1 {input[R1_trimmed]} -2 {input[R2_trimmed]} -o $(dirname $(dirname {output[0]})) --threads {threads} > {log} 
         """

rule correct_error_single_reads_with_spades:
    threads:
        16
    conda:
        pipeline_path + "envs/spades.yml"
    input:
        single_trimmed = "samples/{sample}/reads/trimmed/single.fastq",
    output:
        single_corrected = temp("samples/{sample}/reads/corrected/single.00.0_0.cor.fastq.gz")
    log:
        logging_folder + "samples/{sample}/logs/spades_read_correction.txt"
    shell:
         """
         spades.py --only-error-correction  -s {input[single_trimmed]} -o $( dirname $(dirname {output[single_corrected]})) --threads {threads} > {log} 
         """

rule assemble_genome_corrected_paired_reads_with_spades:
    threads:
        16
    shadow:
        "minimal"
    conda:
        pipeline_path + "envs/spades.yml"
    input:
        R1_corrected = "samples/{sample}/reads/corrected/R1_paired.00.0_0.cor.fastq.gz",
        R2_corrected = "samples/{sample}/reads/corrected/R2_paired.00.0_0.cor.fastq.gz",
        single_corrected = "samples/{sample}/reads/corrected/R_unpaired.00.0_0.cor.fastq.gz",
    output:
        contigs = "samples/{sample}/assembly/spades/contigs.fasta",
        graph = "samples/{sample}/assembly/spades/assembly_graph.fastg",
        scaffolds = "samples/{sample}/assembly/spades/scaffolds.fasta",
        contigs_paths = "samples/{sample}/assembly/spades/contigs.paths",
        scaffolds_paths = "samples/{sample}/assembly/spades/scaffolds.paths",
        log = "samples/{sample}/assembly/spades/spades.log",
    shell:
        """
        spades.py --only-assembler -1 {input[R1_corrected]} -2 {input[R2_corrected]} -s {input[single_corrected]} -o $(dirname {output[contigs]}) --threads {threads} > /dev/null
        """

rule assemble_genome_corrected_single_reads_with_spades:
    threads:
        16
    shadow:
        "minimal"
    conda:
        pipeline_path + "envs/spades.yml"
    input:
        single_corrected = "samples/{sample}/reads/corrected/single.00.0_0.cor.fastq.gz",
    output:
        contigs = "samples/{sample}/assembly/spades/contigs.fasta",
        graph = "samples/{sample}/assembly/spades/assembly_graph.fastg",
        scaffolds = "samples/{sample}/assembly/spades/scaffolds.fasta",
        contigs_paths = "samples/{sample}/assembly/spades/contigs.paths",
        scaffolds_paths = "samples/{sample}/assembly/spades/scaffolds.paths",
        log = "samples/{sample}/assembly/spades/spades.log",
    shell:
        """
        spades.py --only-assembler -s {input[single_corrected]} -o $(dirname {output[contigs]}) --threads {threads} > /dev/null
        """
        
rule assemble_genome_paired_reads_with_spades:
    threads:
        16
    conda:
        pipeline_path + "envs/spades.yml"
    input:
        R1_trimmed = "samples/{sample}/reads/trimmed/R1_paired.fastq",
        R2_trimmed = "samples/{sample}/reads/trimmed/R2_paired.fastq",
    output:
        "samples/{sample}/assembly/spades_no_correction/contigs.fasta"
    log:
        logging_folder + "samples/{sample}/logs/spades_no_correction_assembly.txt"
    shell:
        """
        spades.py --only-assembler -1 {input[R1_trimmed]} -2 {input[R2_trimmed]} -o $( dirname {output[0]}) > {log}
        mv $(dirname {output[0]})/contigs.fasta $(dirname $(dirname {output[0]}))
        rm -rf $( dirname {output[0]})/*
        mv $(dirname $(dirname {output[0]}))/contigs.fasta $(dirname {output[0]})/
        """

rule assemble_genome_single_reads_with_spades:
    threads:
        16
    conda:
        pipeline_path + "envs/spades.yml"
    input:
        single_trimmed = "samples/{sample}/reads/trimmed/single.fastq",
    output:
        "samples/{sample}/assembly/spades_no_correction/contigs.fasta"
    log:
        logging_folder + "samples/{sample}/logs/spades_no_correction_assembly.txt",
    shell:
        """
        spades.py --only-assembler -s {input[single_trimmed]} -o $( dirname {output[0]}) > {log}
        mv $(dirname {output[0]})/contigs.fasta $(dirname $(dirname {output[0]}))
        rm -rf $( dirname {output[0]})/*
        mv $(dirname $(dirname {output[0]}))/contigs.fasta $(dirname {output[0]})/
        """
        
